key_up = "w"
key_down = "s"
key_right = "d"
key_left = "a"
key_sprint = "shift"

window "dimensions" 1200 800
window "show"
window "maximise"

github = "https://raw.githubusercontent.com/Mistium/Mages-And-Magic/main/"

def "position" "x_pos,y_pos"
goto x_pos * screen_zoom y_pos * screen_zoom
endef

def "new_game"
  inventory = "array".new(40)
  inventory.[1] = "stick"
  current_slot = 1

  player_direction = 90
  player_speed_modifier = 1
  player_stretch_x = -100
  target_stretch = -100

  player_can_move = true
  player_speed = 7

  max_hp = 5
  hp = 5
  xp = 0
  player_level = 0
  travel "cave of the lost" 1500 1500
endef

def "travel" "target_island,travel_x,travel_y"
  current_island = target_island
  player_x = travel_x
  player_y = travel_y
endef

def "probar" "width,height,rounding,percent,fill_colour"
  square width height rounding
  if percent != 0 (
    change width / -2
    change width * percent / 2
    square width * percent height rounding : c#fill_colour
  )
endef

def "player_collide"
  c #fff
  if current_island == "cave of the lost" (
    position player_x + 1000 player_y - 2600
    hitbox 200 * screen_zoom 500 * screen_zoom 0 0
    if collided (
      travel "hetrem" 2200 2400
    )
  )
  if current_island == "hetrem" (
    position player_x - 2400 player_y - 2400
    hitbox 200 * screen_zoom 500 * screen_zoom 0 0
    if collided (
     travel "cave of the lost" -900 2600
    )
  )
endef

def "player_animate"
  if key_left.pressed and key_right.pressed.not (
    direction = "a"
    target_stretch = 100
  )

  if key_right.pressed and key_left.pressed.not (
    direction = "d"
    target_stretch = -100
  )

  if key_right.pressed "player_direction = 90"
  if key_left.pressed "player_direction = -90"

  if key_up.pressed and key_down.pressed.not (
    player_direction = 0
    if key_left.pressed "player_direction -= 45"
    if key_right.pressed "player_direction += 45"
  )
  if key_down.pressed and key_up.pressed.not (
    player_direction = 180
    if key_left.pressed "player_direction += 45"
    if key_right.pressed "player_direction -= 45"
  )

  player_stretch_x += target_stretch - player_speed_modifier - player_stretch_x / 5

  dir = 90

  if key_up.pressed or key_down.pressed or key_left.pressed or key_right.pressed (
    dir = ( timer - 3 * 500 )
    dir = dir.cos * 2.5 + 90
  )

endef

def "player_movement"
  if key_sprint.pressed "player_speed = 16" else "player_speed = 12"
  if ( key_left.pressed and key_right.pressed.not ) or ( key_left.pressed.not and key_right.pressed ) (
    player_x_vel += player_direction.sin * player_speed * -0.5
  )
  if ( key_up.pressed and key_down.pressed.not ) or ( key_up.pressed.not and key_down.pressed ) (
    player_y_vel += player_direction.cos * player_speed * -0.5
  )
  smooth_dir += player_direction - smooth_dir / 15
  player_x_vel *= 0.6
  player_y_vel *= 0.6
  last_x = player_x
  last_y = player_y
  player_collide
  if player_can_move (
    player_x += player_x_vel
    player_y += player_y_vel
  )
endef

def "main"
  direction 90
  stretch "x" 100
  position player_x player_y
  stretch "x" 100
  stretch "y" 100
  image github ++ "osl/assets/islands/" ++ current_island ++ "/island.png" 18000 * screen_zoom

  player_animate
  player_movement

  goto 0 0
  stretch "x" player_stretch_x
  direction dir
  image github ++ "osl/assets/players/0.png".str 60 * screen_zoom
  change smooth_dir.sin * 70 * screen_zoom smooth_dir.cos * 70 * screen_zoom
  item = github ++ "Items/Images/" ++ inventory.[current_slot] ++ ".png" 
  image item item.imageinfo("width") * 10 * screen_zoom
endef

def "render_ui"
  direction 90
  stretch "x" 100
  loc 2 -2 80 45
  image github ++ "osl/assets/user-interface/interactable/hotbar.png" 154
  loc -2 2 -155 -105
  if guild_mini (
     image github ++ "osl/assets/user-interface/interactable/Maximised.svg" 300
  )
  change_y 51.5
  if guild_mini.not (
    image github ++ "osl/assets/user-interface/interactable/Minimised.svg" 300
  )
  square 300 100 10 1 1
  if clicked and can (
    can = false
    guild_mini = guild_mini.not
  )
  loc -2 2 -290 -25
  text username 10 : c#000
  loc -2 2 -280 -65
  text player_level 15
  c #92683e
  loc -2 2 -128 -50
  probar 203 20 0 hp / max_hp #d14b41
  loc -2 2 -224 -50
  text "Hp" + hp ++ "/" ++ max_hp 10 : c#000

  max_xp = player_level + 5 * 30
  loc -2 2 -128 -77
  c #92683e
  probar 203 20 0 xp / max_xp #d1cc41
  loc -2 2 -224 -77
  text "Xp" + xp ++ "/" ++ max_xp 10 : c#000
endef

new_game

mainloop:
screen_zoom = window_height / 80
screen_zoom = screen_zoom.clamp(1,9) / 10
main
render_ui

if mouse_down.not "can = true"
loc 2 2 20 -20
text "<player_x.round> <player_y.round> <fps> <screen_zoom>" 10 : c#fff
