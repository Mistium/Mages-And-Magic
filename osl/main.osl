key_up = "w"
key_down = "s"
key_right = "d"
key_left = "a"
key_sprint = "shift"

window "dimensions" 1400 900
screen_zoom = 1

github = "https://raw.githubusercontent.com/Mistium/Mages-And-Magic/main/"

def "new_game"
  inventory = "40".new("array")
  inventory.[1] = "stick"
  current_slot = 1

  player_x = 2000
  player_y = 2200

  player_direction = 90
  player_speed_modifier = 1
  player_stretch_x = -100
  target_stretch = -100

  player_can_move = true
  player_speed = 7

  current_island = "hetrem"
  max_hp = 5
  hp = 5
  xp = 0 
  player_level = 0
endef

def "travel" "target_island,travel_x,travel_y"
  current_island = target_island
  player_x = travel_x
  player_y = travel_y
endef

def "probar" "width,height,rounding,percent,fill_colour"
  square width height rounding
  if percent != 0 (
    change width / -2
    change width * percent / 2
    square width * percent height rounding : c#fill_colour
  )
endef

def "player_collide"
  if current_island == "hetrem" (
    goto camx camy
    change -2400 * screen_zoom -2300 * screen_zoom
    hitbox 200 500 0 0
    if collided "travel "cave of the lost" 0 0"
  )
endef

def "player_animate"
  if key_left.pressed and key_right.pressed.not (
    direction = "a"
    target_stretch = 100
  )

  if key_right.pressed and key_left.pressed.not (
    direction = "d"
    target_stretch = -100
  )

  if key_right.pressed "player_direction = 90"
  if key_left.pressed "player_direction = -90"

  if key_up.pressed and key_down.pressed.not (
    player_direction = 0
    if key_left.pressed "player_direction -= 45"
    if key_right.pressed "player_direction += 45"
  )
  if key_down.pressed and key_up.pressed.not (
    player_direction = 180
    if key_left.pressed "player_direction += 45"
    if key_right.pressed "player_direction -= 45"
  )

  player_stretch_x += target_stretch - player_speed_modifier - player_stretch_x / 5

  dir = 90

  if key_up.pressed or key_down.pressed or key_left.pressed or key_right.pressed (
    dir = ( timer - 3 * 500 )
    dir = dir.cos * 2.5 + 90
  )

endef

def "player_movement"
  if key_sprint.pressed "player_speed = 10" else "player_speed = 7"
  if ( key_left.pressed and key_right.pressed.not ) or ( key_left.pressed.not and key_right.pressed ) (
    player_x_vel += player_direction.sin * player_speed * -0.5
  )
  if ( key_up.pressed and key_down.pressed.not ) or ( key_up.pressed.not and key_down.pressed ) (
    player_y_vel += player_direction.cos * player_speed * -0.5
  )
  player_x_vel *= 0.6
  player_y_vel *= 0.6
  last_x = player_x
  last_y = player_y
  player_collide
  if player_can_move (
    player_x += player_x_vel
    player_y += player_y_vel
  )
endef

def "main"
  direction 90
  stretch "x" 100
  camx = player_x * screen_zoom
  camy = player_y * screen_zoom
  goto camx camy
  image github ++ "osl/assets/islands/" ++ current_island ++ "/island.png" 17006 * screen_zoom

  player_animate
  player_movement

  goto 0 0
  stretch "x" player_stretch_x
  direction dir
  image github ++ "osl/assets/players/0.png".str 50 * screen_zoom
  change 30 * screen_zoom
  image github ++ "Items/Images/" ++ inventory.[current_slot] ++ ".png" 30
endef

def "render_ui"
  direction 90
  stretch "x" 100
  loc 2 -2 80 45
  image github ++ "osl/assets/user-interface/interactable/hotbar.png" 154
  loc -2 2 -155 -105
  if guild_mini (
     image github ++ "osl/assets/user-interface/interactable/Maximised.svg" 300
  )
  change_y 51.5
  if guild_mini.not (
    image github ++ "osl/assets/user-interface/interactable/Minimised.svg" 300
  )
  square 300 100 10 1 1
  if clicked and can (
    can = false
    guild_mini = guild_mini.not
  )
  loc -2 2 -290 -25
  text username 10 : c#000
  loc -2 2 -280 -65
  text player_level 15
  c #92683e
  loc -2 2 -128 -50
  probar 203 20 0 hp / max_hp #d14b41
  loc -2 2 -224 -50
  text "Hp" + hp ++ "/" ++ max_hp 10 : c#000

  max_xp = player_level + 5 * 30
  loc -2 2 -128 -77
  c #92683e
  probar 203 20 0 xp / max_xp #d1cc41
  loc -2 2 -224 -77
  text "Xp" + xp ++ "/" ++ max_xp 10 : c#000
endef

new_game

mainloop:

main
render_ui

if mouse_down.not "can = true"
import "win-buttons"
